# Beast Mastery Hunter
# October 25, 2020

# Level <= 50.

actions.precombat+=/summon_pet
actions.precombat+=/use_item,name=azsharas_font_of_power
actions.precombat+=/worldvein_resonance
actions.precombat+=/guardian_of_azeroth
actions.precombat+=/memory_of_lucid_dreams
actions.precombat+=/use_item,effect_name=cyclotronic_blast,if=!raid_event.invulnerable.exists&(trinket.1.has_cooldown+trinket.2.has_cooldown<2|equipped.variable_intensity_gigavolt_oscillating_reactor)
actions.precombat+=/focused_azerite_beam,if=!raid_event.invulnerable.exists
# Adjusts the duration and cooldown of Aspect of the Wild and Primal Instincts by the duration of an unhasted GCD when they're used precombat. Because Aspect of the Wild reduces GCD by 200ms, this is 1.3 seconds.
actions.precombat+=/aspect_of_the_wild,precast_time=1.3,if=!azerite.primal_instincts.enabled&!essence.essence_of_the_focusing_iris.major&(equipped.azsharas_font_of_power|!equipped.cyclotronic_blast)
# Adjusts the duration and cooldown of Bestial Wrath and Haze of Rage by the duration of an unhasted GCD when they're used precombat.
actions.precombat+=/bestial_wrath,precast_time=1.5,if=azerite.primal_instincts.enabled&!essence.essence_of_the_focusing_iris.major&(equipped.azsharas_font_of_power|!equipped.cyclotronic_blast)

actions+=/counter_shot
actions+=/tranquilizing_shot
actions+=/use_items,if=prev_gcd.1.aspect_of_the_wild|fight_remains<20
actions+=/use_item,name=azsharas_font_of_power,if=cooldown.aspect_of_the_wild.remains_guess<15&fight_remains>10
actions+=/use_item,name=ashvanes_razor_coral,if=debuff.razor_coral_debuff.up&(!equipped.azsharas_font_of_power|trinket.azsharas_font_of_power.cooldown.remains>86|essence.blood_of_the_enemy.major)&(prev_gcd.1.aspect_of_the_wild|!equipped.cyclotronic_blast&buff.aspect_of_the_wild.remains>9)&(!essence.condensed_lifeforce.major|buff.guardian_of_azeroth.up)&(target.health.pct<35|!essence.condensed_lifeforce.major|!talent.killer_instinct.enabled)|(debuff.razor_coral_debuff.down|fight_remains<26)&fight_remains>(24*(cooldown.cyclotronic_blast.remains+4<fight_remains))
actions+=/use_item,effect_name=cyclotronic_blast,if=buff.bestial_wrath.down|fight_remains<5
actions+=/call_action_list,name=cds
actions+=/call_action_list,name=st,if=active_enemies<2
actions+=/call_action_list,name=cleave,if=active_enemies>1

actions.cds=ancestral_call,if=cooldown.bestial_wrath.remains>30
actions.cds+=/fireblood,if=cooldown.bestial_wrath.remains>30
actions.cds+=/berserking,if=buff.aspect_of_the_wild.up&(fight_remains>cooldown.berserking.duration+duration|(target.health.pct<35|!talent.killer_instinct.enabled))|fight_remains<13
actions.cds+=/blood_fury,if=buff.aspect_of_the_wild.up&(fight_remains>cooldown.blood_fury.duration+duration|(target.health.pct<35|!talent.killer_instinct.enabled))|fight_remains<16
actions.cds+=/lights_judgment,if=buff.frenzy.up&buff.frenzy.remains>gcd.max|!buff.frenzy.up
actions.cds+=/potion,if=buff.bestial_wrath.up&buff.aspect_of_the_wild.up&target.health.pct<35|((consumable.potion_of_unbridled_fury|consumable.unbridled_fury)&fight_remains<61|fight_remains<26)
actions.cds+=/worldvein_resonance,if=(prev_gcd.1.aspect_of_the_wild|cooldown.aspect_of_the_wild.remains<gcd|fight_remains<20)|!essence.vision_of_perfection.minor
actions.cds+=/guardian_of_azeroth,if=cooldown.aspect_of_the_wild.remains<10|fight_remains>cooldown+duration|fight_remains<30
actions.cds+=/ripple_in_space
actions.cds+=/memory_of_lucid_dreams
actions.cds+=/reaping_flames,if=target.health.pct>80|target.health.pct<=20|target.time_to_pct_20>30

actions.cleave=barbed_shot,cycle_targets=1,if=buff.frenzy.up&buff.frenzy.remains<=gcd.max|cooldown.bestial_wrath.remains<12+gcd&talent.scent_of_blood.enabled
actions.cleave+=/multishot,if=buff.beast_cleave.remains<0.25+gcd.max
actions.cleave+=/barbed_shot,cycle_targets=1,if=full_recharge_time<gcd.max&cooldown.bestial_wrath.remains
actions.cleave+=/aspect_of_the_wild
actions.cleave+=/stampede,if=buff.aspect_of_the_wild.up&buff.bestial_wrath.up|fight_remains<15
actions.cleave+=/bestial_wrath,if=talent.scent_of_blood.enabled|cooldown.aspect_of_the_wild.remains_guess>20|talent.one_with_the_pack.enabled|fight_remains<15
actions.cleave+=/chimaera_shot
actions.cleave+=/a_murder_of_crows
actions.cleave+=/barrage
actions.cleave+=/kill_command,if=active_enemies<4|!azerite.rapid_reload.enabled
actions.cleave+=/dire_beast
actions.cleave+=/barbed_shot,cycle_targets=1,if=pet.main.buff.frenzy.down&(charges_fractional>1.8|buff.bestial_wrath.up)|cooldown.aspect_of_the_wild.remains<pet.main.buff.frenzy.duration-gcd&azerite.primal_instincts.enabled|charges_fractional>1.4|fight_remains<9
actions.cleave+=/focused_azerite_beam
actions.cleave+=/purifying_blast
actions.cleave+=/concentrated_flame
actions.cleave+=/blood_of_the_enemy
actions.cleave+=/the_unbound_force,if=buff.reckless_force.up|buff.reckless_force_counter.stack<10
actions.cleave+=/multishot,if=azerite.rapid_reload.enabled&active_enemies>2
actions.cleave+=/cobra_shot,if=cooldown.kill_command.remains>focus.time_to_max&(active_enemies<3|!azerite.rapid_reload.enabled)

actions.st=kill_shot
actions.st+=/bloodshed
actions.st+=/barbed_shot,if=buff.frenzy.up&buff.frenzy.remains<gcd|cooldown.bestial_wrath.remains&(full_recharge_time<gcd|azerite.primal_instincts.enabled&cooldown.aspect_of_the_wild.remains<gcd)|cooldown.bestial_wrath.remains<12+gcd&talent.scent_of_blood.enabled
actions.st+=/concentrated_flame,if=focus+focus.regen*gcd<focus.max&buff.bestial_wrath.down&(!dot.concentrated_flame_burn.remains&!action.concentrated_flame.in_flight)|full_recharge_time<gcd|target.time_to_die<5
actions.st+=/aspect_of_the_wild,if=buff.aspect_of_the_wild.down&(cooldown.barbed_shot.charges<1|!azerite.primal_instincts.enabled)
actions.st+=/stampede,if=buff.aspect_of_the_wild.up&buff.bestial_wrath.up|target.time_to_die<15
actions.st+=/a_murder_of_crows
actions.st+=/focused_azerite_beam,if=buff.bestial_wrath.down|target.time_to_die<5
actions.st+=/the_unbound_force,if=buff.reckless_force.up|buff.reckless_force_counter.stack<10|target.time_to_die<5
actions.st+=/bestial_wrath,if=talent.scent_of_blood.enabled|talent.one_with_the_pack.enabled&buff.bestial_wrath.remains<gcd|buff.bestial_wrath.down&cooldown.aspect_of_the_wild.remains>15|fight_remains<15+gcd
actions.st+=/barbed_shot,if=azerite.dance_of_death.rank>1&buff.dance_of_death.remains<gcd
actions.st+=/blood_of_the_enemy,if=buff.aspect_of_the_wild.remains>10+gcd|fight_remains<10+gcd
actions.st+=/kill_command
actions.st+=/bag_of_tricks,if=buff.bestial_wrath.down|fight_remains<5
actions.st+=/chimaera_shot
actions.st+=/dire_beast
actions.st+=/barbed_shot,if=talent.one_with_the_pack.enabled&charges_fractional>1.5|charges_fractional>1.8|cooldown.aspect_of_the_wild.remains<pet.main.buff.frenzy.duration-gcd&azerite.primal_instincts.enabled|fight_remains<9
actions.st+=/purifying_blast,if=buff.bestial_wrath.down|fight_remains<8
actions.st+=/barrage
actions.st+=/cobra_shot,if=(focus-cost+focus.regen*(cooldown.kill_command.remains-1)>action.kill_command.cost|cooldown.kill_command.remains>1+gcd&cooldown.bestial_wrath.remains_guess>focus.time_to_max|buff.memory_of_lucid_dreams.up)&cooldown.kill_command.remains>1|fight_remains<3
actions.st+=/barbed_shot,if=pet.main.buff.frenzy.duration-gcd>full_recharge_time