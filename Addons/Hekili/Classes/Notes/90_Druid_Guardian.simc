# Guardian Druid
# October 29, 2020

# Changes:
# - Incorporate Cat/Moonkin priorities (if setting is checked).

# Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/kindred_spirits
# actions.precombat+=/cat_form,if=druid.catweave_bear&talent.feral_affinity.enabled
actions.precombat+=/prowl,if=druid.catweave_bear&talent.feral_affinity.enabled&buff.cat_form.up
actions.precombat+=/bear_form,if=!druid.catweave_bear&!druid.owlweave_bear
actions.precombat+=/heart_of_the_wild,if=talent.heart_of_the_wild.enabled&druid.owlweave_bear&buff.moonkin_form.up
actions.precombat+=/wrath,if=druid.owlweave_bear&buff.moonkin_form.up

# Executed every time the actor is available.
actions=skull_bash
actions+=/use_items
actions+=/potion,if=((talent.heart_of_the_wild.enabled&buff.heart_of_the_wild.up)&(druid.catweave_bear|druid.owlweave_bear))
actions+=/run_action_list,name=catweave,if=druid.catweave_bear&((cooldown.thrash_bear.remains>0&cooldown.mangle.remains>0&dot.moonfire.remains>=gcd+0.5&rage<40&buff.incarnation_guardian_of_ursoc.down&buff.berserk_bear.down&buff.galactic_guardian.down)|(buff.cat_form.up&energy>25)|(runeforge.oath_of_the_elder_druid.equipped&!buff.oath_of_the_elder_druid.up&(buff.cat_form.up&energy>20))|(runeforge.oath_of_the_elder_druid.equipped&buff.heart_of_the_wild.remains<10)&(buff.cat_form.up&energy>20)|(covenant.kyrian&cooldown.empower_bond.remains<=1&active_enemies<2))
actions+=/run_action_list,name=owlweave,if=druid.owlweave_bear&((cooldown.thrash_bear.remains>0&cooldown.mangle.remains>0&rage<20&buff.incarnation.down&buff.berserk_bear.down)|(buff.moonkin_form.up&dot.sunfire.refreshable)|(buff.moonkin_form.up&buff.heart_of_the_wild.up)|(buff.moonkin_form.up&(buff.eclipse_lunar.up|buff.eclipse_solar.up)&!runeforge.oath_of_the_elder_druid.equipped)|(runeforge.oath_of_the_elder_druid.equipped&!buff.oath_of_the_elder_druid.up)|(covenant.night_fae&cooldown.convoke_the_spirits.remains<=1)|(covenant.kyrian&cooldown.empower_bond.remains<=1&active_enemies<2))
actions+=/run_action_list,name=lycarao,if=((runeforge.lycaras_fleeting_glimpse.equipped)&(talent.balance_affinity.enabled)&(buff.lycaras_fleeting_glimpse.up)&(buff.lycaras_fleeting_glimpse.remains<=2))
actions+=/run_action_list,name=lycarac,if=((runeforge.lycaras_fleeting_glimpse.equipped)&(talent.feral_affinity.enabled)&(buff.lycaras_fleeting_glimpse.up)&(buff.lycaras_fleeting_glimpse.remains<=2))
actions+=/call_action_list,name=mitigation
actions+=/run_action_list,name=bear

actions.bear=bear_form,if=!buff.bear_form.up
actions.bear+=/potion,if=((buff.berserk_bear.up|buff.incarnation_guardian_of_ursoc.up)&(!druid.catweave_bear&!druid.owlweave_bear))
actions.bear+=/ravenous_frenzy
actions.bear+=/convoke_the_spirits,if=!druid.catweave_bear&!druid.owlweave_bear
actions.bear+=/berserk_bear,if=(buff.ravenous_frenzy.up|!covenant.venthyr)
actions.bear+=/incarnation,if=(buff.ravenous_frenzy.up|!covenant.venthyr)
actions.bear+=/empower_bond,if=(!druid.catweave_bear&!druid.owlweave_bear)|active_enemies>=2
actions.bear+=/barkskin,if=(talent.brambles.enabled)&(buff.bear_form.up)
actions.bear+=/ironfur,if=buff.ironfur.remains<0.5
actions.bear+=/adaptive_swarm,cycle_targets=1,if=refreshable
actions.bear+=/moonfire,if=(buff.galactic_guardian.up&druid.owlweave_bear)&active_enemies<=3
actions.bear+=/thrash_bear,cycle_targets=1,if=refreshable|dot.thrash_bear.stack<3|(dot.thrash_bear.stack<4&runeforge.luffainfused_embrace.equipped)|active_enemies>5
actions.bear+=/swipe,if=buff.incarnation_guardian_of_ursoc.down&buff.berserk_bear.down&active_enemies>=4
actions.bear+=/maul,if=buff.incarnation.up&active_enemies<2
actions.bear+=/maul,if=(buff.savage_combatant.stack>=1)&(buff.tooth_and_claw.up)&buff.incarnation.up&active_enemies=2
actions.bear+=/mangle,if=buff.incarnation.up&active_enemies<=3
actions.bear+=/moonfire,cycle_targets=1,if=refreshable&active_enemies<=3
actions.bear+=/maul,if=(buff.tooth_and_claw.stack>=2)|(buff.tooth_and_claw.up&buff.tooth_and_claw.remains<1.5)|(buff.savage_combatant.stack>=3)
actions.bear+=/thrash_bear,if=active_enemies>1
actions.bear+=/moonfire,if=(buff.galactic_guardian.up&druid.catweave_bear)&active_enemies<=3|(buff.galactic_guardian.up&!druid.catweave_bear&!druid.owlweave_bear)&active_enemies<=3
actions.bear+=/mangle,if=(rage<80)&active_enemies<4
actions.bear+=/pulverize,cycle_targets=1,if=dot.thrash_bear.stack>2
actions.bear+=/thrash_bear
actions.bear+=/maul
actions.bear+=/swipe_bear

# actions+=/call_action_list,name=mitigation
# actions+=/wild_charge
# actions+=/call_action_list,name=cooldowns
# actions+=/maul,if=rage.deficit<20&active_enemies<4
# actions+=/maul,if=rage.deficit<20&essence.conflict_and_strife.major&!buff.sharpened_claws.up
# actions+=/ironfur,if=cost=0|(rage>cost&azerite.layered_mane.enabled&active_enemies>2)
# actions+=/pulverize,target_if=dot.thrash_bear.stack=dot.thrash_bear.max_stacks
# actions+=/moonfire,target_if=dot.moonfire.refreshable&active_enemies<2
# actions+=/thrash,if=(buff.incarnation.down&active_enemies>1)|(buff.incarnation.up&active_enemies>4)
# actions+=/swipe,if=buff.incarnation.down&active_enemies>4
# actions+=/mangle,if=dot.thrash_bear.ticking
# actions+=/moonfire,target_if=buff.galactic_guardian.up&active_enemies<2
# actions+=/thrash
# Fill with Moonfire with PotMx2
# actions+=/moonfire,if=azerite.power_of_the_moon.rank>1&active_enemies=1
# actions+=/swipe

actions.mitigation=ironfur,if=incoming_damage_5s>ironfur_damage_threshold&buff.ironfur.remains<3*haste
actions.mitigation+=/renewal,if=tanking&health.pct<65
actions.mitigation+=/frenzied_regeneration,if=tanking&health.pct<65
actions.mitigation+=/anima_of_death,if=toggle.defensives&health.pct<80
actions.mitigation+=/barkskin,if=(talent.brambles.enabled)&(buff.bear_form.up)&health.pct<75&incoming_damage_5s>0.25*health.max
actions.mitigation+=/survival_instincts,if=buff.survival_instincts.down&buff.barkskin.down&health.pct<60&incoming_damage_5s>0.25*health.max

actions.cooldowns=potion
actions.cooldowns+=/heart_essence
actions.cooldowns+=/blood_fury
actions.cooldowns+=/berserking
actions.cooldowns+=/arcane_torrent
actions.cooldowns+=/lights_judgment
actions.cooldowns+=/fireblood
actions.cooldowns+=/ancestral_call
actions.cooldowns+=/bristling_fur,if=buff.bear_form.up
actions.cooldowns+=/incarnation,if=(dot.moonfire.ticking|active_enemies>1)&dot.thrash_bear.ticking
actions.cooldowns+=/berserk
actions.cooldowns+=/use_item,effect_name=cyclotronic_blast
actions.cooldowns+=/anima_of_death,if=toggle.cooldowns&(time_to_die<cooldown%2|active_enemies>1)
actions.cooldowns+=/memory_of_lucid_dreams
actions.cooldowns+=/use_items

actions.catweave=prowl
actions.catweave+=/rake,if=buff.prowl.up
actions.catweave+=/heart_of_the_wild,if=talent.heart_of_the_wild.enabled&!buff.heart_of_the_wild.up
actions.catweave+=/empower_bond,if=druid.catweave_bear
actions.catweave+=/convoke_the_spirits,if=druid.catweave_bear
actions.catweave+=/rip,if=dot.rip.refreshable&combo_points>=4
actions.catweave+=/ferocious_bite,if=combo_points>=4
actions.catweave+=/adaptive_swarm,cycle_targets=1,if=refreshable
actions.catweave+=/rake,if=dot.rake.refreshable&combo_points<4
actions.catweave+=/thrash_cat,if=active_enemies>1&active_dot.thrash_cat=0
actions.catweave+=/shred,if=combo_points<5

actions.lycarac=cat_form

actions.lycarao=moonkin_form

actions.owlweave=heart_of_the_wild,if=talent.heart_of_the_wild.enabled&!buff.heart_of_the_wild.up
actions.owlweave+=/empower_bond,if=druid.owlweave_bear
actions.owlweave+=/convoke_the_spirits,if=druid.owlweave_bear
actions.owlweave+=/adaptive_swarm,cycle_targets=1,if=refreshable
actions.owlweave+=/moonfire,cycle_targets=1,if=refreshable|buff.galactic_guardian.up
actions.owlweave+=/sunfire,cycle_targets=1,if=refreshable
actions.owlweave+=/starsurge,if=(buff.eclipse_lunar.up|buff.eclipse_solar.up)
actions.owlweave+=/starfire,if=(eclipse.in_lunar|eclipse.solar_next)|(eclipse.in_lunar&buff.eclipse_lunar.empowered)
actions.owlweave+=/wrath