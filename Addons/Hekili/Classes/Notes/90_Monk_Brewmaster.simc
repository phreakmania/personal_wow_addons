# Brewmaster Monk
# October 30, 2020

# Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/fleshcraft
actions.precombat+=/chi_burst
actions.precombat+=/chi_wave

# Executed every time the actor is available.
actions=spear_hand_strike

actions+=/purifying_brew,if=settings.purify_for_celestial&(time_to_max_charges<gcd.max|buff.purified_chi.up&buff.purified_chi.remains<1.5*gcd.max)|cooldown.celestial_brew.remains<2*gcd.max&charges_fractional>1.5
actions+=/celestial_brew,if=buff.purified_chi.up
actions+=/dampen_harm,if=health.pct<75&incoming_damage_3s>health.max*(0.2+(0.2*group))&buff.fortifying_brew.down
actions+=/fortifying_brew,if=health.pct<50&incoming_damage_3s>health.max*(0.2+(0.2*group))&(buff.dampen_harm.down)

actions+=/use_item,name=ashvanes_razor_coral,if=debuff.razor_coral_debuff.down|debuff.conductive_ink_debuff.up&target.health.pct<31|time_to_die<20
actions+=/use_items
actions+=/potion
actions+=/blood_fury
actions+=/berserking
actions+=/lights_judgment
actions+=/fireblood
actions+=/ancestral_call
actions+=/invoke_niuzao_the_black_ox,if=time_to_die>25

# Purifying behaviour is based on normalization (iE the late expression triggers if stagger size increased over the last 30 ticks or 15 seconds).
# actions+=/purifying_brew,if=settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&charges=charges_max&stagger.heavy&(buff.purified_chi.down|buff.purified_chi.remains<1.5*gcd)&cooldown.celestial_brew.remains<5

# Purifying behaviour is based on normalization.
# actions+=/purifying_brew,if=settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>18
# actions+=/purifying_brew,if=settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>9&stagger.amount_to_total_pct>53
# actions+=/purifying_brew,if=settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>4.5&stagger.amount_to_total_pct>71
# actions+=/purifying_brew,if=settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>3&stagger.amount_to_total_pct>80

# Use configured purify_stagger_currhp from Brewmaster options.
actions+=/purifying_brew,if=settings.purify_stagger_currhp>0&group&stagger.pct>=settings.purify_stagger_currhp
actions+=/purifying_brew,if=settings.purify_stagger_currhp>0&solo&stagger.pct>=settings.purify_stagger_currhp*0.5

# Use configured purify_stagger_maxhp from Brewmaster options.
actions+=/purifying_brew,if=settings.purify_stagger_maxhp>0&group&stagger.pct>=settings.purify_stagger_maxhp
actions+=/purifying_brew,if=settings.purify_stagger_maxhp>0&solo&stagger.pct>=settings.purify_stagger_maxhp*0.5

# Stagger % of 20 will kill you in 2.5 seconds without healing or purifying.  Used when purify_stagger_currhp and maxhp are disabled.
actions+=/purifying_brew,if=settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&group&stagger.pct>20
actions+=/purifying_brew,if=settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&solo&stagger.pct>10

# Healing Elixir
actions+=/expel_harm,if=health.pct<5*healing_sphere.count|(healing_sphere.count>=3&health.pct<50)
actions+=/healing_elixir,if=health.pct<33

# Black Ox Brew is currently used to either replenish brews based on less than half a brew charge available, or low energy to enable Keg Smash
actions+=/black_ox_brew,if=cooldown.purifying_brew.charges_fractional<0.5
actions+=/black_ox_brew,if=(energy+(energy.regen*cooldown.keg_smash.remains))<40&buff.blackout_combo.down&cooldown.keg_smash.up

# Celestial Brew priority whenever it took significant damage and ironskin brew buff is missing (adjust the health.max coefficient according to intensity of damage taken), and to dump excess charges before BoB.
actions+=/celestial_brew,if=tanking&buff.blackout_combo.down&incoming_damage_2999ms>(health.max*0.05+stagger.last_tick_damage_4)&buff.elusive_brawler.stack<2
actions+=/celestial_brew,if=tanking&buff.purified_chi.up&(buff.purified_chi.remains<1.5*gcd|cooldown.purifying_brew.remains>buff.purified_chi.remains)

# Offensively, the APL prioritizes KS on cleave, BoS else, with energy spenders and cds sorted below
actions+=/keg_smash,if=spell_targets>=2

actions+=/detox
actions+=/rushing_jade_wind,if=spell_targets>=2&buff.rushing_jade_wind.down

actions+=/bonedust_brew
actions+=/fallen_order
actions+=/faeline_stomp
actions+=/weapons_of_order
actions+=/tiger_palm,if=talent.rushing_jade_wind.enabled&buff.blackout_combo.up&buff.rushing_jade_wind.up
actions+=/tiger_palm,if=buff.blackout_combo.up
actions+=/expel_harm,if=buff.gift_of_the_ox.stack>4
actions+=/blackout_kick
actions+=/keg_smash
actions+=/concentrated_flame,if=dot.concentrated_flame.remains=0
actions+=/heart_essence,if=!essence.the_crucible_of_flame.major
actions+=/expel_harm,if=buff.gift_of_the_ox.stack>=3
actions+=/touch_of_death
actions+=/rushing_jade_wind,if=buff.rushing_jade_wind.down
actions+=/breath_of_fire,if=buff.blackout_combo.down&(buff.bloodlust.down|(buff.bloodlust.up&dot.breath_of_fire_dot.refreshable))
actions+=/chi_burst
actions+=/chi_wave
# Expel Harm has higher DPET than TP when you have at least 2 orbs.
actions+=/expel_harm,if=buff.gift_of_the_ox.stack>=2
actions+=/spinning_crane_kick,if=active_enemies>=3&cooldown.keg_smash.remains>execute_time&(energy+(energy.regen*(cooldown.keg_smash.remains+execute_time)))>=65
actions+=/tiger_palm,if=!talent.blackout_combo.enabled&cooldown.keg_smash.remains>gcd&(energy+(energy.regen*(cooldown.keg_smash.remains+gcd)))>=65
actions+=/arcane_torrent,if=energy<31
actions+=/rushing_jade_wind