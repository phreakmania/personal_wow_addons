# Demonology Warlock
# https://github.com/simulationcraft/simc/
# October 26, 2020

# Changes:
# - Spell Lock.
# - Only recommend Demonbolt pre-combat if it is instant (or hardcast for bosses).
# - Power Siphon allowed pre-combat to proc instant Demonbolt.

# Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/fel_domination,if=time>0&!pet.alive
actions.precombat+=/summon_pet
actions.precombat+=/power_siphon,if=buff.wild_imps.stack>1&buff.demonic_core.stack<=2
actions.precombat+=/demonbolt,if=boss|action.demonbolt.cast=0
actions.precombat+=/shadow_bolt
actions.precombat+=/variable,name=tyrant_ready,value=cooldown.summon_demonic_tyrant.remains<5&(!talent.doom.enabled|debuff.doom.up)&(buff.demonic_strength.up|!talent.demonic_strength.enabled|talent.demonic_consumption.enabled)&(!talent.nether_portal.enabled|buff.nether_portal.up|cooldown.nether_portal.remains>15)&(!talent.grimoire_felguard.enabled|cooldown.grimoire_felguard.remains>15)&(!talent.summon_vilefiend.enabled|cooldown.summon_vilefiend.remains>15)&(!talent.demonic_consumption.enabled|buff.wild_imps.up|soul_shard=5|buff.nether_portal.up&soul_shard>0)

# Executed every time the actor is available.
actions=spell_lock
actions+=/axe_toss
actions+=/devour_magic
actions+=/call_action_list,name=off_gcd
actions+=/call_action_list,name=essences
actions+=/run_action_list,name=tyrant_prep,strict=1,if=cooldown.summon_demonic_tyrant.remains<5&!variable.tyrant_ready
actions+=/run_action_list,name=summon_tyrant,strict=1,if=variable.tyrant_ready
actions+=/scouring_tithe
actions+=/impending_catastrophe
actions+=/soul_rot,if=active_enemies>1
actions+=/decimating_bolt,if=(target.health_pct<50|fight_remains>50)
actions+=/summon_vilefiend,if=cooldown.summon_demonic_tyrant.remains>40|fight_remains<cooldown.summon_demonic_tyrant.remains+25
actions+=/call_dreadstalkers
actions+=/doom,if=refreshable
actions+=/demonic_strength
actions+=/bilescourge_bombers
actions+=/hand_of_guldan,if=soul_shard=5|buff.nether_portal.up
actions+=/hand_of_guldan,if=soul_shard>=3&cooldown.summon_demonic_tyrant.remains>20&(cooldown.summon_vilefiend.remains>5|!talent.summon_vilefiend.enabled)&cooldown.call_dreadstalkers.remains>2
actions+=/demonbolt,if=buff.demonic_core.react&soul_shard<4
actions+=/grimoire_felguard,if=cooldown.summon_demonic_tyrant.remains+cooldown.summon_demonic_tyrant.duration>fight_remains|fight_remains<cooldown.summon_demonic_tyrant.remains+15
actions+=/use_items
actions+=/power_siphon,if=buff.wild_imps.stack>1&buff.demonic_core.stack<3
actions+=/implosion,if=azerite.explosive_potential.rank>1&buff.explosive_potential.remains<3&buff.wild_imps.stack>=3
actions+=/soul_strike
actions+=/shadow_bolt

actions.essences=worldvein_resonance,if=cooldown.summon_demonic_tyrant.remains>45
actions.essences+=/memory_of_lucid_dreams
actions.essences+=/blood_of_the_enemy
actions.essences+=/guardian_of_azeroth
actions.essences+=/ripple_in_space
actions.essences+=/focused_azerite_beam
actions.essences+=/purifying_blast
actions.essences+=/reaping_flames,if=target.health_pct>80|target.health_pct<20|fight_remains<30|target.time_to_pct_20>40
actions.essences+=/concentrated_flame
actions.essences+=/the_unbound_force,if=buff.reckless_force.remains

actions.off_gcd=berserking,if=pet.demonic_tyrant.active
actions.off_gcd+=/potion,if=buff.berserking.up|pet.demonic_tyrant.active&!race.troll
actions.off_gcd+=/blood_fury,if=pet.demonic_tyrant.active
actions.off_gcd+=/fireblood,if=pet.demonic_tyrant.active

actions.summon_tyrant=hand_of_guldan,if=soul_shard=5,line_cd=20
actions.summon_tyrant+=/demonbolt,if=buff.demonic_core.up&(talent.demonic_consumption.enabled|buff.nether_portal.down),line_cd=20
actions.summon_tyrant+=/shadow_bolt,if=buff.wild_imps.stack+incoming_imps<4&(talent.demonic_consumption.enabled|buff.nether_portal.down),line_cd=20
actions.summon_tyrant+=/call_dreadstalkers
actions.summon_tyrant+=/hand_of_guldan
actions.summon_tyrant+=/demonbolt,if=buff.demonic_core.up&buff.nether_portal.up&((buff.vilefiend.remains>5|!talent.summon_vilefiend.enabled)&(buff.grimoire_felguard.remains>5|buff.grimoire_felguard.down))
actions.summon_tyrant+=/shadow_bolt,if=buff.nether_portal.up&((buff.vilefiend.remains>5|!talent.summon_vilefiend.enabled)&(buff.grimoire_felguard.remains>5|buff.grimoire_felguard.down))
# actions.summon_tyrant+=/variable,name=tyrant_ready,value=!cooldown.summon_demonic_tyrant.ready
actions.summon_tyrant+=/summon_demonic_tyrant,if=soul_shards=0
actions.summon_tyrant+=/shadow_bolt

actions.tyrant_prep=doom,line_cd=30
actions.tyrant_prep+=/demonic_strength,if=!talent.demonic_consumption.enabled
actions.tyrant_prep+=/nether_portal
actions.tyrant_prep+=/grimoire_felguard
actions.tyrant_prep+=/summon_vilefiend
actions.tyrant_prep+=/call_dreadstalkers
actions.tyrant_prep+=/demonbolt,if=buff.demonic_core.up&soul_shard<4&(talent.demonic_consumption.enabled|buff.nether_portal.down)
actions.tyrant_prep+=/shadow_bolt,if=soul_shard<5-4*buff.nether_portal.up
# actions.tyrant_prep+=/variable,name=tyrant_ready,value=1
actions.tyrant_prep+=/hand_of_guldan