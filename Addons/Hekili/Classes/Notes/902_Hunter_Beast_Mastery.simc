# Beast Mastery Hunter
# October 13, 2020

# Level > 50.

actions.precombat+=/summon_pet
actions.precombat+=/tar_trap,if=runeforge.soulforge_embers.equipped
actions.precombat+=/aspect_of_the_wild,precast_time=1.3
actions.precombat+=/bestial_wrath,precast_time=1.5,if=!talent.scent_of_blood.enabled&!runeforge.soulforge_embers.equipped

actions+=/counter_shot
actions+=/tranquilizing_shot
actions+=/use_items,if=prev_gcd.1.aspect_of_the_wild|fight_remains<20
actions+=/call_action_list,name=cds
actions+=/call_action_list,name=st,if=active_enemies<2
actions+=/call_action_list,name=cleave,if=active_enemies>1

actions.cds+=/ancestral_call,if=cooldown.bestial_wrath.remains>30
actions.cds+=/fireblood,if=cooldown.bestial_wrath.remains>30
actions.cds+=/berserking,if=buff.aspect_of_the_wild.up&(fight_remains>cooldown.berserking.duration+duration|(target.health.pct<35|!talent.killer_instinct.enabled))|fight_remains<13
actions.cds+=/blood_fury,if=buff.aspect_of_the_wild.up&(fight_remains>cooldown.blood_fury.duration+duration|(target.health.pct<35|!talent.killer_instinct.enabled))|fight_remains<16
actions.cds+=/lights_judgment,if=pet.main.buff.frenzy.up&pet.main.buff.frenzy.remains>gcd.max|!pet.main.buff.frenzy.up
actions.cds+=/potion,if=buff.bestial_wrath.up&buff.aspect_of_the_wild.up&target.health.pct<35|((consumable.potion_of_unbridled_fury|consumable.unbridled_fury)&fight_remains<61|fight_remains<26)

actions.cleave+=/barbed_shot,cycle_targets=1,if=pet.main.buff.frenzy.up&pet.main.buff.frenzy.remains<=gcd
actions.cleave+=/multishot,if=pet.main.buff.beast_cleave.remains<0.25+gcd
actions.cleave+=/tar_trap,if=runeforge.soulforge_embers.equipped&tar_trap.remains<gcd&cooldown.flare.remains<gcd
actions.cleave+=/flare,if=tar_trap.up
actions.cleave+=/death_chakram,if=focus+cast_regen<focus.max
actions.cleave+=/wild_spirits
actions.cleave+=/barbed_shot,cycle_targets=1,if=full_recharge_time<gcd&cooldown.bestial_wrath.remains|cooldown.bestial_wrath.remains<12+gcd&talent.scent_of_blood.enabled
actions.cleave+=/aspect_of_the_wild
actions.cleave+=/bestial_wrath
actions.cleave+=/resonating_arrow
actions.cleave+=/stampede,if=buff.aspect_of_the_wild.up|fight_remains<15
actions.cleave+=/flayed_shot
actions.cleave+=/kill_shot
actions.cleave+=/chimaera_shot
actions.cleave+=/bloodshed
actions.cleave+=/a_murder_of_crows
actions.cleave+=/barrage,if=pet.main.buff.frenzy.remains>execute_time
actions.cleave+=/kill_command,if=focus>cost+action.multishot.cost
actions.cleave+=/bag_of_tricks,if=buff.bestial_wrath.down|fight_remains<5
actions.cleave+=/dire_beast
actions.cleave+=/barbed_shot,cycle_targets=1,if=fight_remains<9
actions.cleave+=/arcane_shot,if=focus.time_to_max<gcd*2

actions.st+=/barbed_shot,if=pet.main.buff.frenzy.up&pet.main.buff.frenzy.remains<=gcd|full_recharge_time<gcd&cooldown.bestial_wrath.remains|cooldown.bestial_wrath.remains<12+gcd&talent.scent_of_blood.enabled
actions.st+=/tar_trap,if=runeforge.soulforge_embers.equipped&tar_trap.remains<gcd&cooldown.flare.remains<gcd
actions.st+=/flare,if=tar_trap.up
actions.st+=/wild_spirits
actions.st+=/kill_shot
actions.st+=/flayed_shot
actions.st+=/death_chakram,if=focus+cast_regen<focus.max
actions.st+=/bloodshed
actions.st+=/aspect_of_the_wild
actions.st+=/stampede,if=buff.aspect_of_the_wild.up|fight_remains<15
actions.st+=/a_murder_of_crows
actions.st+=/bestial_wrath
actions.st+=/resonating_arrow
actions.st+=/chimaera_shot
actions.st+=/kill_command
actions.st+=/bag_of_tricks,if=buff.bestial_wrath.down|fight_remains<5
actions.st+=/dire_beast
actions.st+=/barbed_shot,if=fight_remains<9
actions.st+=/barrage
actions.st+=/cobra_shot,if=(focus-cost+focus.regen*(cooldown.kill_command.remains-1)>action.kill_command.cost|cooldown.kill_command.remains>1+gcd&cooldown.bestial_wrath.remains_guess>focus.time_to_max)&cooldown.kill_command.remains>1|fight_remains<3